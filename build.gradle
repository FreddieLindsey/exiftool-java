plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'signing'

    id "com.palantir.git-version" version "0.11.0"
}

group 'io.github.freddielindsey'
version gitVersion()

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Exiftool Java Wrapper'
                packaging 'jar'
                artifactId 'exiftool-java'
                description 'Java wrapper around Phil Harvey\'s exiftool - https://www.sno.phy.queensu.ca/~phil/exiftool'
                url 'https://github.com/FreddieLindsey/exiftool-java'

                scm {
                    connection 'git@github.com:FreddieLindsey/exiftool-java'
                    developerConnection 'git@github.com:FreddieLindsey/exiftool-java'
                    url 'https://github.com/FreddieLindsey/exiftool-java'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'freddie.lindsey'
                        name 'Freddie Lindsey'
                        email 'freddie.a.lindsey@gmail.com'
                    }
                }
            }
        }
    }
}
